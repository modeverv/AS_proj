<?xml version="1.0" encoding="utf-8"?>

<!--
	Adobe Systems Incorporated(r) Source Code License Agreement
	Copyright(c) 2008 Adobe Systems Incorporated. All rights reserved.
	
	Please read this Source Code License Agreement carefully before using
	the source code.
	
	Adobe Systems Incorporated grants to you a perpetual, worldwide, non-exclusive, 
	no-charge, royalty-free, irrevocable copyright license, to reproduce,
	prepare derivative works of, publicly display, publicly perform, and
	distribute this source code and such derivative works in source or 
	object code form without any attribution requirements.  
	
	The name "Adobe Systems Incorporated" must not be used to endorse or promote products
	derived from the source code without prior written permission.
	
	You agree to indemnify, hold harmless and defend Adobe Systems Incorporated from and
	against any loss, damage, claims or lawsuits, including attorney's 
	fees that arise or result from your use or distribution of the source 
	code.
	
	THIS SOURCE CODE IS PROVIDED "AS IS" AND "WITH ALL FAULTS", WITHOUT 
	ANY TECHNICAL SUPPORT OR ANY EXPRESSED OR IMPLIED WARRANTIES, INCLUDING,
	BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
	FOR A PARTICULAR PURPOSE ARE DISCLAIMED.  ALSO, THERE IS NO WARRANTY OF 
	NON-INFRINGEMENT, TITLE OR QUIET ENJOYMENT.  IN NO EVENT SHALL MACROMEDIA
	OR ITS SUPPLIERS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
	EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, 
	PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
	OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, 
	WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOURCE CODE, EVEN IF
	ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
-->

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:views="com.adobe.apprise.views.*" creationComplete="onCreationComplete();">
	<mx:Script>
		<![CDATA[
			import com.adobe.apprise.database.DatabaseEvent;
			import com.adobe.apprise.database.DatabaseResponder;
			import mx.events.MenuEvent;

			import com.adobe.apprise.views.CustomHost;
			import com.adobe.apprise.util.AppriseUtils;
			import mx.collections.ArrayCollection;
			import com.adobe.apprise.events.AggregateEvent;
			import com.adobe.apprise.model.ModelLocator;
			import com.adobe.air.alert.NativeAlert;

			private function onCreationComplete():void
			{
				var host:CustomHost = new CustomHost();
				this.html.htmlHost = host;
				this.html.addEventListener(Event.LOCATION_CHANGE, onLocationChange);
			}

			private function onLocationChange(e:Event):void
			{
				feedList.enabled = false;
				feedList.dataProvider = null;
				this.dispatchEvent(new Event(Event.LOCATION_CHANGE));
			}

			private function loadUrl():void
			{
				if (locationBar.text.length == 0) return;
				if (locationBar.text.search(/^http(s?):\/\/.+$/) == -1)
				{
					locationBar.text = ("http://" + locationBar.text);
				}
				html.location = locationBar.text;
			}
			
			private function onHtmlLoadComplete(e:Event):void
			{
				this.dispatchEvent(new Event(Event.COMPLETE));
				backButton.enabled = (html.htmlLoader.historyLength > 0 && html.htmlLoader.historyPosition > 0) ? true : false ;
				forwardButton.enabled = (html.htmlLoader.historyLength > 0 && html.htmlLoader.historyPosition != (html.htmlLoader.historyLength - 1)) ? true : false ;
				var feedUrls:Array = AppriseUtils.getFeedUrls(html.htmlLoader);
				var feeds:ArrayCollection = new ArrayCollection();
				for each (var feed:Object in feedUrls)
				{
					if (feed.title != null)
					{
						feeds.addItem({"label":feed.title,"href":feed.href});
					}
				}
				if (feeds.length != 0)
				{
					feedList.dataProvider = feeds;
					feedList.enabled = true;
				}
			}
			
			private function onAddFeed(e:MenuEvent):void
			{				
				var url:String = e.item.href;
				if (url != null)
				{
					var responder:DatabaseResponder = new DatabaseResponder();
					responder.addEventListener(DatabaseEvent.RESULT_EVENT,
						function(e:DatabaseEvent):void
						{
							if (e.data == -1)
							{
								var aggregateEvent:AggregateEvent = new AggregateEvent();
								aggregateEvent.feeds = [url];
								aggregateEvent.dispatch();
							}
							else
							{
								NativeAlert.show(resourceManager.getString('resources', 'BROWSER_DUPLICATE_FEED_MESSAGE'),
												 resourceManager.getString('resources', 'BROWSER_DUPLICATE_FEED_TITLE'),
												 NativeAlert.OK,
												 true,
												 NativeApplication.nativeApplication.openedWindows[0]);
							}
						});
					ModelLocator.getInstance().db.getFeedIdByFeedUrl(responder, url);
				}
			}
			
			private function filterLocation(loc:String):String
			{
				if (loc == "app:/Apprise.swf")
				{
					return "";
				}
				return loc;
			}

		]]>
	</mx:Script>
	<mx:VBox width="100%" height="100%" verticalAlign="top" verticalGap="0">
		<mx:HBox styleName="browserBar" width="100%" height="60" verticalAlign="middle">
			<mx:HBox horizontalGap="0" verticalAlign="middle">
				<mx:Button id="backButton" width="30" height="30" styleName="left" icon="@Embed(source='assets/arrow_left.png')" click="html.htmlLoader.historyBack()" enabled="false"/>
				<mx:Button id="forwardButton" width="30" height="30" styleName="right" icon="@Embed(source='assets/arrow_right.png')" click="html.htmlLoader.historyForward()" enabled="false"/>
			</mx:HBox>
			<mx:Spacer width="5"/>
			<views:CustomTextInput width="90%" id="locationBar" text="{filterLocation(html.location)}" enter="loadUrl();"/>
			<mx:Button id="loadButton" width="10%" label="{resourceManager.getString('resources', 'BROWSER_LOAD_BUTTON')}" click="loadUrl();" enabled="{locationBar.text.length &gt; 0}"/>
			<mx:PopUpMenuButton id="feedList" width="75" label="RSS" openAlways="false" itemClick="onAddFeed(event);" enabled="false"/>
		</mx:HBox>
		<mx:HTML id="html" width="100%" height="100%" complete="onHtmlLoadComplete(event)"/>
	</mx:VBox>
</mx:Canvas>
