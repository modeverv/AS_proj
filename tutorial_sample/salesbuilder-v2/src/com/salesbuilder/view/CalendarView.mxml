<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ilog="http://www.ilog.com/2007/ilog/flex" xmlns:controls="com.salesbuilder.controls.*"
	width="100%" height="100%" horizontalGap="12"
	icon="@Embed('/assets/Calendar.png')"
	label="Calendar"
	creationComplete="completeHandler()">

	<mx:Script>

		<![CDATA[

		import com.salesbuilder.events.EventManager;
		import com.salesbuilder.events.DataSyncEvent;
		import com.salesbuilder.dao.ICalendarItemDAO;
		import com.salesbuilder.dao.CalendarItemDAO;
		import com.salesbuilder.model.CalendarItemModel;
		import mx.collections.ArrayCollection;
		import mx.events.ItemClickEvent;
		import ilog.utils.TimeUnit;
		import ilog.calendar.CalendarEvent;

		/**
		 * A specific implementation of the ICalendarItemDAO interface to manipulate CalendarItem data.
		 * For a more flexible implementation, the view should be decoupled from a specific implementation
		 * of ICalendarItemDAO. You could use a "dependency lookup" or a "dependency injection" approach 
		 * to provide the DAO to the view.
		 */
		private var calendarItemDAO:ICalendarItemDAO = new CalendarItemDAO();

		[Bindable] public var items:ArrayCollection;
		
		[Bindable] private var currentItem:CalendarItemModel;
		
		private var calendarModes:Array = [
        	{label: "Day", mode: Calendar.DAY_MODE},
        	{label: "Work", mode: Calendar.WORK_WEEK_MODE},
        	{label: "Week", mode: Calendar.WEEK_MODE},
        	{label: "Month", mode: Calendar.MONTH_MODE} ];        

		private function completeHandler():void
		{
			EventManager.addEventListener(DataSyncEvent.COMPLETE,
				function(event:Event):void
				{
					loadData();
				});

			toggleModeBar.dataProvider = calendarModes; 
			loadData();
		}

		private function loadData():void
		{
			items = calendarItemDAO.findAll();
		}

		private function keyboardEventHandler(event:KeyboardEvent):void
		{
	        switch(event.keyCode) 
	        {
				case Keyboard.DELETE:
            		items.removeItemAt(items.getItemIndex(currentItem));
            		calendarItemDAO.remove(currentItem);
            		currentItem = null;
            		break;
	        	
				case Keyboard.ENTER:
		            createItemFromSelection(cal.timeRangeSelection);
		            cal.timeRangeSelection = null;
		            summary.setFocus();
		            break;
		            
				default:
            		var s:String = String.fromCharCode(event.charCode);                       
            		var e:Date = cal.calendar.addUnits(cal.endDisplayedDate, TimeUnit.DAY, 1);
              
		            if (cal.timeRangeSelection != null && 
		               (cal.timeRangeSelection[0] > e || 
		                cal.startDisplayedDate > cal.timeRangeSelection[1])) {
						return;        
		            }             
		            
		            if ((s >= "a" && s <= "z") || (s >= "A" && s <= "A")) 
		            {
						createItemFromSelection(cal.timeRangeSelection, s);              
		              	summary.setFocus();
		              	if (cal.timeRangeSelection != null) 
		              	{
		                	summary.text = s;
		              	}
		              	summary.setSelection(1,1);
		              	cal.timeRangeSelection = null;
		            }
        	}
		}

		private function createItemFromSelection(selection:Array, summary:String=""):void 
		{
			var selection:Array = cal.timeRangeSelection;
			if (selection != null) 
			{
				createItem(selection[0] as Date, selection[1] as Date, summary);
			} 
      	}

		private function createItem(startTime:Date, endTime:Date, summary:String=""):void 
		{
			var newItem:CalendarItemModel = new CalendarItemModel();
			newItem.startTime = startTime;
			newItem.endTime = endTime;
			newItem.summary = summary;
			items.addItem(newItem);
			cal.selectedItems = [newItem];
			currentItem = newItem;
			calendarItemDAO.save(newItem);
      	}

		private function itemSelectionChange(event:CalendarEvent):void 
		{
	        if (cal.selectedItems.length == 1) 
	        {
				currentItem = cal.selectedItems[0];
				callLater(selectSummary)
	        }
	        else
	        {
				currentItem = null;
				description.htmlText = "";
	        }
      	}
		
		private function itemEditEnd(event:CalendarEvent):void 
		{
			callLater(updateItem, [currentItem]);
		}

		private function updateItem(item:CalendarItemModel):void
		{
			calendarItemDAO.save(item);
		}

		private function selectSummary():void
		{
			summary.setFocus();
			summary.setSelection(0, summary.text.length);
		}

		private function summaryChange():void
		{
			currentItem.summary = summary.text;
	        calendarItemDAO.save(currentItem);
		}

		private function descriptionChange():void
		{
			currentItem.description = description.htmlText;
	        calendarItemDAO.save(currentItem);
		}
		
		private function toggleCalendarMode(event:ItemClickEvent):void 
		{
        	if (cal.date == null) 
        	{          
        		cal.date = cal.endDate;              
        	}
        	cal.mode = event.item.mode;
      	}

		private function startDateChange():void
		{
			var temp:Date = startDate.selectedDate;
			temp.hours = startTime.selectedItem.hours;
			temp.minutes = startTime.selectedItem.minutes;

			// If the new start time is after the initial end time, we assume that the user wants
			// to move the meeting keeping its original duration as opposed to simply changing the
			// start time.  
			if (temp > currentItem.endTime)
			{
				var diff:Number = currentItem.endTime.time - currentItem.startTime.time;
				currentItem.startTime = temp;
				currentItem.endTime = new Date(temp.time + diff); 
			}
			else
			{
				currentItem.startTime = temp;
			}
	        calendarItemDAO.save(currentItem);
	        cal.executeBindings();
		}
		
		private function endDateChange():void
		{
			var temp:Date = endDate.selectedDate;
			temp.hours = endTime.selectedItem.hours;
			temp.minutes = endTime.selectedItem.minutes;
			
			// If the new end time is before the initial start time, we assume that the user wants
			// to move the meeting keeping its original duration as opposed to simply changing the
			// end time.  
			if (temp < currentItem.startTime)
			{
				var diff:Number = currentItem.endTime.time - currentItem.startTime.time;
				currentItem.endTime = temp;
				currentItem.startTime = new Date(temp.time - diff); 
			}
			else
			{
				currentItem.endTime = temp;
			}	
	        calendarItemDAO.save(currentItem);
	        cal.executeBindings();
		}
		
		]]>
	</mx:Script>


	<ilog:Calendar width="100%" height="100%" id="cal" mode="week" doubleClickEnabled="true" 
		keyUp="keyboardEventHandler(event)" dataProvider="{items}"
		itemEditEnd="itemEditEnd(event)"
      	change="itemSelectionChange(event)">
	</ilog:Calendar>
	
	<mx:VBox width="260" height="100%" paddingRight="8" paddingTop="8" verticalGap="4">
		<mx:ToggleButtonBar id="toggleModeBar" itemClick="toggleCalendarMode(event)" width="100%"/>            
		<mx:DateChooser id="dc" yearNavigationEnabled="true" change="cal.date = dc.selectedDate" width="100%" height="180"/>
		<mx:Form width="100%" paddingLeft="0" verticalGap="4" paddingTop="4" paddingBottom="4">
			<mx:FormItem label="Start:" direction="horizontal">
				<mx:DateField id="startDate" selectedDate="{currentItem.startTime}" change="startDateChange()" enabled="{currentItem != null}"/>
				<controls:TimeComboBox id="startTime" date="{currentItem.startTime}" change="startDateChange()" enabled="{currentItem != null}"/>
			</mx:FormItem>
			<mx:FormItem label="End:" direction="horizontal">
				<mx:DateField id="endDate" selectedDate="{currentItem.endTime}" change="endDateChange()" enabled="{currentItem != null}"/>
				<controls:TimeComboBox id="endTime" date="{currentItem.endTime}" change="endDateChange()" enabled="{currentItem != null}"/>
			</mx:FormItem>
		</mx:Form>
		<mx:Label text="Summary:"/>
		<mx:TextArea id="summary" width="100%" height="40" text="{currentItem.summary}" change="summaryChange()" enabled="{currentItem != null}"/>			
		<mx:Label text="Description:"/>
		<mx:RichTextEditor id="description" title="Description" width="100%" height="100%" htmlText="{currentItem.description}" change="descriptionChange()" enabled="{currentItem != null}"
			headerHeight="0" borderThicknessBottom="0" 
			borderThicknessTop="0" borderThicknessLeft="0" borderThicknessRight="0" dropShadowEnabled="false" borderAlpha="0"
			minHeight="180"/>			
	</mx:VBox>

</mx:HBox>
